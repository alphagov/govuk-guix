#!/usr/bin/guile --no-auto-compile
-*- scheme -*-
!#

;; To allow this script to be run directly, even from the Git
;; repository, check if the environment is setup by checking if the
;; (gds services) module is loadable. If it's not loadable, start this
;; script again, but use the guix-pre-inst-env helper to setup the
;; environment.
(catch
  #t
  (lambda ()
    (resolve-interface '(gds services)))
  (lambda args
    (let* ((govuk-guix-root
            (or (and=> (current-filename)
                       (lambda (x)
                         (dirname (dirname x))))
                (getenv "GOVUK_GUIX_ROOT")
                (error "Unable to locate the govuk-guix root")))
           (command-full-path
            (string-append govuk-guix-root "/bin/govuk-system")))
      (exit
       (status:exit-val
        (apply
         system*
         "bash"
         (string-append govuk-guix-root "/guix-pre-inst-env")
         "guile"
         command-full-path
         (cdr (command-line))))))))

(use-modules
 (srfi srfi-1)
 (srfi srfi-26)
 (srfi srfi-37)
 (ice-9 match)
 (ice-9 rdelim)
 (guix ui)
 (guix monads)
 (guix scripts)
 (guix scripts build)
 (guix derivations)
 (guix grafts)
 (guix store)
 (guix build utils)
 (gnu services)
 (gnu services web)
 (gnu system)
 (gnu system linux-container)
 (gnu system file-systems)
 (gds utils)
 (gds services rails)
 (gds services govuk)
 (gds services govuk plek)
 (gds services govuk nginx)
 (gds services govuk routing-configuration)
 (gds services utils databases)
 (gds systems utils)
 (gds systems govuk base)
 (gds systems govuk development))

(define %build-default-options
  ;; Alist of default option values.
  `((graft? . #t)
    (substitutes? . #t)
    (build-hook? . #t)
    (print-build-trace? . #t)
    (verbosity . 0)))

(define %default-options
  (let ((data-dir (or (getenv "XDG_DATA_HOME")
                      (and=> (getenv "HOME")
                             (cut string-append <> "/.local/share")))))
    `(,@(if (file-exists? data-dir)
            (let ((var/lib-dir
                   (string-append
                    data-dir
                    "/govuk-guix/systems/development/state/var/lib")))
              (mkdir-p var/lib-dir)
              (list
               (cons
                'file-system-mapping
                (file-system-mapping (source var/lib-dir)
                                     (target "/var/lib")
                                     (writable? #t)))))
            '())
      (rails-environment . "development")
      (app-domain . "dev.gov.uk")
      (web-domain . "dev.gov.uk")
      (use-high-ports? . #t)
      (use-https? . #f)
      ,@%build-default-options)))

(define (service-names->service-types service-names)
  (map
   (lambda (service-name)
     (or (find (lambda (service-type)
                 (eq?
                  (string->symbol service-name)
                  (service-type-name service-type)))
               (map service-kind govuk-services))
         (begin
           (let
               ((similarly-named-services
                 (find-similar-strings service-name
                                       (map (lambda (service)
                                              (symbol->string
                                               (service-type-name
                                                (service-kind service))))
                                            govuk-services))))
             (if (null? similarly-named-services)
                 (leave (G_ "No service called ~A") service-name)
                 (leave (G_ "No service called ~A, did you mean ~A?")
                        service-name (first similarly-named-services)))))))
   service-names))

(define* (operating-system-for-services
          service-names
          rails-environment
          app-domain
          web-domain
          use-high-ports
          use-https)

  (define service-types-whitelist
    (append
     (if (null? service-names)
         (map service-kind govuk-services)
         (service-names->service-types service-names))
     (cons*
      govuk-nginx-service-type
      ;; TODO: The authenticating-proxy service isn't required by
      ;; anything.
      authenticating-proxy-service-type
      asset-manager-service-type
      (map service-kind base-services))))

  (define base-system
    (update-system-services-package-source-from-environment
     (system-without-unnecessary-services
      (filter
       (lambda (service)
         (member (service-kind service) service-types-whitelist))
       (operating-system-user-services govuk-development-os))
      govuk-development-os)))

  (define routing-configuration-arguments
    `(#:use-high-ports? ,use-high-ports
      #:use-https? ,use-https
      #:app-domain ,app-domain
      #:web-domain ,web-domain))

  (define plek-config
    (apply plek-config-from-routing-configuration-arguments
           (operating-system-user-services base-system)
           routing-configuration-arguments))

  (define database-service-ports
    (if use-high-ports
        high-database-service-ports
        default-database-service-ports))

  (define (add-database-startup-scripts services)
    (map ensure-database-user-exists-on-service-startup
         (map run-db:setup-if-postgresql-or-mysql-is-used services)))

  (define (set-services-configuration services)
    (add-database-startup-scripts
     (apply
      set-routing-configuration-for-services
      (map
       (cut update-rails-app-config-environment-for-service rails-environment <>)
       services)
      routing-configuration-arguments)))

  (operating-system
    (inherit base-system)
    (hosts-file (plek-config->hosts-file plek-config))
    (packages useful-packages)
    (services (set-services-configuration
               (operating-system-user-services base-system)))))

(define options
  ;; Specifications of the command-line options.
  (cons* (option '("share") #t #f
                 (lambda (opt name arg result)
                   (alist-cons 'file-system-mapping
                               (specification->file-system-mapping arg #t)
                               result)))
         (option '("no-data-persistence") #f #f
                 (lambda (opt name arg result)
                   (remove (match-lambda
                             (('file-system-mapping . value)
                              (string=? (file-system-mapping-target value)
                                      "/var/lib"))
                             ((key . value) #f))
                           result)))
         (option '("rails-environment") #t #f
                 (lambda (opt name arg result . rest)
                   (apply values
                          (alist-cons 'rails-environment
                                      arg
                                      (alist-delete 'rails-environment result))
                          rest)))
         (option '("app-domain") #t #f
                 (lambda (opt name arg result . rest)
                   (apply values
                          (alist-cons 'app-domain
                                      arg
                                      (alist-delete 'app-domain result))
                          rest)))
         (option '("web-domain") #t #f
                 (lambda (opt name arg result . rest)
                   (apply values
                          (alist-cons 'web-domain
                                      arg
                                      (alist-delete 'web-domain result))
                          rest)))
         (option '("use-high-ports") #f #t
                 (lambda (opt name arg result . rest)
                   (apply values
                          (alist-cons 'use-high-ports?
                                      (string=? arg "true")
                                      (alist-delete 'use-high-ports? result))
                          rest)))
         (option '("use-https") #f #t
                 (lambda (opt name arg result . rest)
                   (apply values
                          (alist-cons
                           'use-https?
                           (or (eq? arg #f)
                               (if (member arg '("development" "certbot"))
                                   (string->symbol arg)
                                   (error "Unknown https argument" arg)))
                           (alist-delete 'use-https? result))
                          rest)))
         %standard-build-options))

(define (start . args)
  (define (option-values opts key)
    (reverse
     (filter-map (match-lambda
                   ((head . tail)
                    (and (eq? key head) tail))
                   (_ #f))
                 opts)))

  (define (start-script opts)
    (with-store store
      (set-build-options-from-command-line store opts)

      (run-with-store store
        (mbegin %store-monad
          (set-grafting #f)
          (mlet* %store-monad
              ((sys (container-script
                     (operating-system-for-services
                      (option-values opts 'argument)
                      (assq-ref opts 'rails-environment)
                      (assq-ref opts 'app-domain)
                      (assq-ref opts 'web-domain)
                      (assq-ref opts 'use-high-ports?)
                      (assq-ref opts 'use-https?))
                     #:mappings (option-values opts 'file-system-mapping)
                     #:container-shared-network? #t)))
            (mbegin %store-monad
              (built-derivations (list sys))
              (return (derivation->output-path sys))))))))

  (define (sudo-path)
    (find
     file-exists?
     '("/run/setuid-programs/sudo"
       "/usr/bin/sudo")))

  (let* ((opts (parse-command-line args options (list %default-options))))
    (exit
     (status:exit-val
      (apply
       system*
       (if (eq? (getuid) 1)
           (list (start-script opts))
           (list
            (sudo-path)
            (start-script opts))))))))

(define (passphrase . args)
  (or (and=> (getenv "GOVUK_GUIX_DEVELOPMENT_PASSPHRASE")
             (lambda (passphrase)
               (simple-format
                #t
                "The passphrase (set through GOVUK_GUIX_DEVELOPMENT_PASSPHRASE) is: ~A"
                passphrase)))
      (let ((data-dir (or (getenv "XDG_DATA_HOME")
                          (and=> (getenv "HOME")
                                 (cut string-append <> "/.local/share")))))
        (if (file-exists? data-dir)
            (let* ((passphrase-file
                    (string-append
                     data-dir
                     "/govuk-guix/systems/development/passphrase")))
              (if (file-exists? passphrase-file)
                  (let ((passphrase
                         (call-with-input-file passphrase-file read-line)))
                    (simple-format
                     #t
                     "The passphrase for the development system is recorded in ~A. It is:\n\n  ~A\n\n"
                     passphrase-file
                     passphrase))
                  (leave
                   (G_ "The passphrase file could not be found at ~A")
                   passphrase-file)))
            (leave
             (G_ "The data directory could not be determined"))))))

(define (govuk-system . args)
  (define commands
    `(("start" . ,start)
      ("passphrase" . ,passphrase)))

  (display "\n")
  (with-error-handling
    (if (= (length args) 1)
        (leave (G_ "no command specified"))
        (let* ((command (second args))
               (handler
                (assoc-ref commands command)))
          (if handler
              (apply handler (cddr args))
              (leave (G_ "command ~A is not recognised") command))))))

(apply govuk-system (command-line))
